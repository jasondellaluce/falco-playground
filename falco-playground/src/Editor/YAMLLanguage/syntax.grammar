@top Document { document }

@skip { space | newlineEmpty | Comment }

document { newline Property* }

Property { (ConditionKey ConditionValue newline) | (Key Value newline) }

ConditionValue { Scalar | MultiLine }

Value { Scalar | MultiLine | Mapping | Sequence }

Scalar { Boolean | Null | Number | String }
String { PlainString | QuotedString }

MultiLine {
  (quote lineSkip newline multiline quote) |
  (FoldOp newline multiline)
}

@skip { newlineEmpty } {
  multiline { indent (lineSkip newline)+ dedent }
}

Mapping { "{" commaSep<Scalar>? "}" }

Sequence { "[" commaSep<Scalar>? "]" }

Boolean { @specialize<PlainString, "true" | "false" > }
Null { @specialize<PlainString, "null" | "~" > }

@context trackIndent from "./tokens.js"

@external tokens indentation from "./tokens" { indent, dedent }

@tokens {
  DocStart { "---" }
  DocEnd { "..." }
  colon { ": " }
  FoldOp { $[|>] $[+\-]? }

  ConditionKey { "condition" space? colon }
  Key { keyChar (std.digit | keyChar)* space? colon }
  keyChar { std.asciiLetter | "_" }

  Number { "-"? (int | int? frac) exp? }
  int { "0" | $[1-9] std.digit* }
  frac { "." (std.digit+ | "nan" | "inf") }
  exp { $[eE] $[+\-]? std.digit+ }

  QuotedString { quote line quote }
  PlainString { ![{}[\]&*#?|\-<>=!%@:" \t\f] line }
  lineSkip { (!["\n\r] | "\\" _)+ }
  line { (![#"\n\r] | "\\" _)+ }

  quote { '"' }
  Comment { "#" ![\n\r]* }
  space { ($[ \t\f] | "\\" $[\n\r])+ }

  "{" "}" "[" "]"

  @precedence { ConditionKey, Key }
  @precedence { Number, PlainString }
  @precedence { lineSkip, space }
  @precedence { lineSkip, Comment }
}

commaSep<expr> { expr ("," expr)* }

@external tokens newlines from "./tokens" { newline, newlineEmpty }

@external propSource yamlHighlighting from "./highlight"

@detectDelim
